plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.3'
	id 'io.spring.dependency-management' version '1.1.3'

	id 'com.diffplug.spotless' version '6.19.0'
}

group = 'net.decathlon'
version = '0.3.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_20

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}


var incubatorArguments = ["--enable-preview","--add-modules", "jdk.incubator.concurrent"]
tasks.withType(JavaCompile) { options.compilerArgs += incubatorArguments }
tasks.withType(Test) { jvmArgs += incubatorArguments }

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
	java {
		importOrder()
		removeUnusedImports()
		cleanthat()
		googleJavaFormat().aosp().reflowLongStrings()
		formatAnnotations()
		toggleOffOn()
		licenseHeader '/* Decathlon Italy - Tacos Team(C) $YEAR */'
	}
}

tasks.register('updateGitHooks', Copy) {
	from './scripts/pre-commit'
	into './.git/hooks'
}
compileJava.dependsOn updateGitHooks
